tmpl robot(id: String): watering, collectFruit, trim {
    (r: Robot)[{ id: id }]
    (m: move)[?]
    (cf: collectFruit)[?]
    (w: watering)[?]
    (t: trim)[?]

    m -->* cf, w, t
    cf --><> w, t
    w --><> cf, t
    t --><> cf, w
} => w, cf, t

tmpl plant(id: String, position: { x: Number, y: Number }, plantType: { typename: String }, wp: watering) {
    (p:Plant)[{ id: id, plantType: plantType, position: position }]
    (nw: needWatering)[?]

    nw -->% nw
    nw *--> wp
    wp -->+ nw
}

%(pts: PlantTypes)[[{ typename: "apple" }, { typename: "orange" }, { typename: "banana" }]]

(ar: addRobot)[?: { id: String }]

ar -->> {
    robot(id = @trigger.value.id) => watering, collectFruit, trim

    (ap: addPlant)[?: { id: String, typename: String, position: { x: Number, y: Number } }]
    
    foreach plantType in pts.value:
        ap -->> {
            if plantType.typename == @trigger.value.typename:
                plant(
                    id = @trigger.value.id,
                    position = @trigger.value.position,
                    plantType = plantType,
                    wp -> watering
                )
            /if
        }
    /foreach
}
