# ---- Review request process -------------------------- #
#                                                        #
# This scenario models the similar process from the      #
# `main-no-tmpls.tdcr` file, but using templates to      #
# generate the approver process.                         #
#                                                        #
# ------------------------------------------------------ #

# ---- Template definition ----------------------------- #

tmpl reviewer_process(n: Number): Approve, Reject {
  (r: Reviewer)[n]
  !(rv: Review)[?: Unit]
  (ap: Approve)[?: Unit]
  (rj: Reject)[?: Unit]
  
  rv -->* ap
  rv -->* rj
  ap -->% rj
  rj -->% ap
} => ap, rj

# ---- Process ------------------------------------------ #

# Initialize pull request with N reviewers.
(pr: PullRequest)[?: Number]

pr -->> {
  (sa: SetApproved)[?: Unit]
  %(sr: SetChangesRequested)[?: Unit]
  
  foreach i in @range(1, @trigger.value) {
    reviewer_process(n = i) => approve, reject
    
    approve -->* sa
    reject -->% sa
    reject -->+ sr
  }
}
